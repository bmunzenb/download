buildscript {
    ext {
        kotlin = '2.1.0'
        gradle_versions = '0.51.0'
        okio = '3.10.0'
        clikt = '5.0.2'
        junit = '4.13.2'
        mockk = '1.13.14'
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "com.github.ben-manes.versions" version "$gradle_versions"
    id "org.jetbrains.kotlin.jvm" version "$kotlin"
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    version = '1.0'

    kotlin {
        jvmToolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    base {
        archivesName = "${rootProject.name}-${project.name}"
        group = "com.munzenberger"
    }

    dependencies {
        testImplementation "junit:junit:$junit"
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    // disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}