plugins {
    id 'java-library'
    id 'maven-publish'
}

dependencies {
    api "com.squareup.okio:okio:$okio"

    testImplementation "io.mockk:mockk:$mockk"
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/bmunzenb/download")
            credentials {
                username = System.getenv("GITHUB_USER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = "${rootProject.name}-${project.name}"

            pom {
                name = 'Download Core'
                description = 'Mass download utilities'
                url = 'https://github.com/bmunzenb/download'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit'
                    }
                }
                scm {
                    url = 'https://github.com/bmunzenb/download'
                }
            }

            from components.java
        }
    }
}
